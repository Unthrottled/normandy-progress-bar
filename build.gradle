def properties(String key) {
  return project.findProperty(key).toString()
}

plugins {
  id 'org.jetbrains.intellij' version '1.0'
  id "org.jetbrains.kotlin.jvm" version "1.5.10"
  id 'org.kordamp.gradle.markdown' version '2.2.0'
  id "org.jlleitschuh.gradle.ktlint" version "10.0.0"
}
repositories {
  mavenCentral()
}

group 'io.unthrottled'
version '1.9.10'

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation 'commons-io:commons-io:2.10.0'
  implementation 'io.sentry:sentry:5.0.1'
  implementation "com.google.code.gson:gson:2.8.7"
}

configurations {
  implementation.exclude group: 'org.slf4j'
}

intellij {
  version.set('LATEST-EAP-SNAPSHOT')
  type.set('IC')
  if (System.getenv().getOrDefault("ENV", "") == "DEV") {
    plugins.set(
      Collections.emptyList()
//      Collections.singletonList('io.acari.DDLCTheme:15.1.0')
    )
  }
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

markdownToHtml {
  sourceDir file("$projectDir/docs")
  outputDir file("$projectDir/build/html")
}

tasks.runIde {
  idePath = properties("idePath")
  if (!idePath.isEmpty()) {
    ideDir.set(file(idePath))
  }
}

tasks.patchPluginXml {
  def releaseNotes = "$projectDir/build/html/RELEASE-NOTES.html"
  if (file(releaseNotes).exists()) {
    changeNotes.set(file(releaseNotes).text)
  }
  sinceBuild.set(System.getenv("pluginSinceBuild") ?: sinceBuildP)
  untilBuild.set(System.getenv("pluginUntilBuild") ?: untilBuildP)
}

tasks.patchPluginXml.dependsOn("markdownToHtml")

plugins {
  id 'org.jetbrains.intellij' version '0.4.16'
  id "org.jetbrains.kotlin.jvm" version "1.4.0"
  id 'org.kordamp.gradle.markdown' version '2.2.0'
  id "org.jlleitschuh.gradle.ktlint" version "8.2.0"
}
repositories {
  mavenCentral()
}

group 'io.unthrottled'
version '1.9.6'

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation 'commons-io:commons-io:2.6'
  implementation 'io.sentry:sentry:1.7.30'
  implementation "com.google.code.gson:gson:2.8.5"
}

configurations {
  implementation.exclude group: 'org.slf4j'
}

intellij {
  version '2020.2'
  type 'IU'
  alternativeIdePath idePath

  if (System.getenv().getOrDefault("ENV", "") == "DEV") {
    plugins(
      'io.acari.DDLCTheme:10.0.0'
    )
  }
}

compileKotlin {
  kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
  kotlinOptions.jvmTarget = "1.8"
}

markdownToHtml {
  sourceDir file("$projectDir/docs")
  outputDir file("$projectDir/build/html")
}

patchPluginXml {
  def releaseNotes = "$projectDir/build/html/RELEASE-NOTES.html"
  if (file(releaseNotes).exists()) {
    changeNotes file(releaseNotes).text
  }
  sinceBuild System.getenv("pluginSinceBuild") ?: sinceBuildP
  untilBuild System.getenv("pluginUntilBuild") ?: untilBuildP
}

tasks.patchPluginXml.dependsOn("markdownToHtml")
